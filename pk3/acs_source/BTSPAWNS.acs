#library "BTSPAWNS"
#include "zcommon.acs"

script "BTLMS_ENTER" ENTER // invulnerability script
{
	int timeStart = 36*GetCVar("BTLMS_InvulnTime");
	int timeOn = timeStart;
	bool renderType = false;
	bool hasEnded = false;
	int buttons = GetPlayerInput(-1, INPUT_BUTTONS);
	
	if(CheckInventory("WasInvulnerable")>0 || timeStart <= 0)
	{
		terminate;
	}
	
	Delay(1);
	
	if(GetCVar("BTLMS_InvulnBlast"))
		GiveInventory("LMSRepulsion", 1);
	
	SetActorProperty(0, APROP_INVULNERABLE, true);
	while(!hasEnded)
	{
		if(timeOn%9==0 && timeOn!=0)
		{
			if(!renderType)
			{
				SetActorProperty(0, APROP_RENDERSTYLE, STYLE_Translucent);
				SetActorProperty(0, APROP_ALPHA, 0.8);
			}
			else
			{
				SetActorProperty(0, APROP_RENDERSTYLE, STYLE_Fuzzy);
			}
			
			renderType = !renderType;
		}
		
		if((buttons & BT_ATTACK || buttons & BT_ALTATTACK) && (timeStart-timeOn >= 10))
		{
			hasEnded = true;
		}
		
		if(timeOn == 0 || GetActorProperty(0, APROP_HEALTH) <= 0)
		{
			hasEnded = true;
		}
		
		Delay(1);
		buttons = GetPlayerInput(-1, INPUT_BUTTONS);
		timeOn--;
	}
	SetActorProperty(0, APROP_RENDERSTYLE, STYLE_Translucent);
	SetActorProperty(0, APROP_ALPHA, 1.0);
	SetActorProperty(0, APROP_INVULNERABLE, false);
	GiveInventory("WasInvulnerable", 1);
}

script "BTLMS_RESPAWN" RESPAWN // Calls the previous script on respawn
{
	ACS_NamedExecuteAlways("BTLMS_ENTER", 0);
}

script "BTLMS_TEAMSPAWNS" OPEN
{
	if(GetCVar("BTLMS_InvulnTime") > 0) {
		if(!GetCVar("sv_norespawninvul")) {
			ConsoleCommand("sv_norespawninvul true");
		}
	}

	if(GetCVar("BTLMS_OverrideTeamStarts"))
	{
		ConsoleCommand("sv_useteamstartsindm true");
		Delay(5*35);
		if(GetGameModeState()==GAMESTATE_INPROGRESS)
		{
			ConsoleCommand("sv_useteamstartsindm false");
		}
		while(GetGameModeState()==GAMESTATE_INPROGRESS)
		{
			Delay(1);
		}
		ConsoleCommand("sv_useteamstartsindm true");
	}
}
