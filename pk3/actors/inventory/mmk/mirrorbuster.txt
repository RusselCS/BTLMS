actor MirrorBusterWep : MegaBuster 10081
{
//$Category MM8BDM-Weapons
Weapon.AmmoUse 3
Weapon.AmmoGive 28
Weapon.SlotNumber 7
Inventory.Pickupmessage "$PU_MIRRORBUSTER"
Obituary "$OB_MIRRORBUSTER"
weapon.ammotype "MirrorBusterAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
inventory.icon "MIRRSI"
Scale 2.0
States
{
Spawn:
WEA3 H 1
loop
Ready:
MIRR F 0 ACS_ExecuteAlways(998,0,52)
MIRR F 0 A_JumpIfInventory("MirrorBusterFlag", 1, "NoAmmo")
MIRR F 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
MIRR F 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
MIRR F 1 A_Raise
Loop
Fire:
MIRR F 0 A_JumpIfNoAmmo("NoAmmo")
Goto Hold
Hold:
MIRR F 0 A_JumpIfInventory("MirrorBusterAmmo", 1, 1)
Goto NoAmmo
MIRR F 0 A_JumpIfInventory("MirrorBusterFlag", 1, "Hold2")
MIRR F 0 A_JumpIfInventory("ShieldCheck", 1, "CoolDown")
MIRR F 0 A_PlayWeaponSound("weapon/mirroractivate")
MIRR F 0 A_TakeInventory("MirrorBusterAmmo",2)
MIRR F 0 A_GiveInventory("MirrorBusterFlag", 35)
MIRR F 0 A_GiveInventory("ShieldCheck", 1)
Goto Hold2
Hold2:
MIRR H 0 A_JumpIfInventory("MirrorBusterSwitch", 1, "Hold3")
MIRR HHH 1 A_SpawnItemEx("MirrorBuster1",38,0,0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
MIRR G 0 A_TakeInventory("MirrorBusterFlag", 2)
MIRR G 0 A_GiveInventory("MirrorBusterSwitch", 1)
MIRR G 0 A_Refire
MIRR G 0 A_TakeInventory("ShieldCheck", 1)
MIRR G 4 A_TakeInventory("MirrorBusterFlag", 35)
Goto Ready+1
Hold3:
MIRR HHH 1 A_SpawnItemEx("MirrorBuster2",38,0,0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
MIRR G 0 A_TakeInventory("MirrorBusterFlag", 2)
MIRR G 0 A_TakeInventory("MirrorBusterSwitch", 1)
MIRR G 0 A_Refire
MIRR G 0 A_TakeInventory("ShieldCheck", 1)
MIRR G 4 A_TakeInventory("MirrorBusterFlag", 35)
Goto Ready+1
NoAmmo:
MIRR F 1 ACS_Execute(979,0)
Goto CoolDown+2
CoolDown:
MIRR GF 15
MIRR G 0 A_TakeInventory("ShieldCheck", 1)
MIRR G 0 A_TakeInventory("MirrorBusterFlag", 35)
MIRR G 0 A_ClearRefire
Goto Ready+1
}
}

actor MirrorBusterAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}

actor MirrorBusterFlag : Inventory
{
inventory.amount 1
inventory.maxamount 35
}

actor MirrorBusterSwitch : Inventory
{
inventory.amount 1
inventory.maxamount 1
}


actor MirrorBuster1 
{
+MISSILE
//+RIPPER
+DONTSPLASH
+SHOOTABLE
+GHOST
+NOBLOOD
+NOTARGETSWITCH
+DONTRIP
+DONTBLAST
Damage 0
Height 48
speed 0
health 100
painchance 256
Radius 32
scale 2.5
States
{
Spawn:
MIRR A 2
stop
Pain.PharaohHold:
Pain.StarCrash:
Pain.TopSpin:
Pain.FireSpin:
Pain.Tango:
Pain.GeminiLaser:
Pain.JunkShield:
TNT1 A 0
stop
Pain:
TNT1 A 0 
TNT1 A 0 A_JumpIfHealthLower(76,"Shot2")
TNT1 A 0 A_JumpIfHealthLower(89,"Shot1")
TNT1 A 0 A_JumpIfHealthLower(100,"Shot3")
stop
Shot1:
TNT1 A 0 A_ChangeFlag("INVULNERABLE",1)
TNT1 A 1 A_GiveToTarget("MirrorShot1",1)
stop
Shot2:
TNT1 A 0 A_ChangeFlag("INVULNERABLE",1)
TNT1 A 1 A_GiveToTarget("MirrorShot2",1)
stop
Shot3:
TNT1 A 0 A_ChangeFlag("INVULNERABLE",1)
TNT1 A 1 A_GiveToTarget("MirrorShot3",1)
stop
Death:
TNT1 A 0 A_ChangeFlag("INVULNERABLE",1)
TNT1 A 0 A_JumpIfHealthLower(1,"Shot2")
stop
NoShot:
TNT1 A 0
stop
}
}

actor MirrorBuster2 : MirrorBuster1
{
States
{
Spawn:
MIRR B 2
stop
}
}

actor MirrorShot1 : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
states
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_FireCustomMissile("MirrorShotWeak",0,0,0,0)
TNT1 A 0 A_TakeInventory("MirrorBusterAmmo",2)
TNT1 A 0 A_GiveInventory("MirrorBusterFlag", 35)
TNT1 A 0 A_PlaySound("weapon/mirrordeflect")
stop
}
}

actor MirrorShot2 : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
states
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_FireCustomMissile("MirrorShotStrong",0,0,0,0)
TNT1 A 0 A_TakeInventory("MirrorBusterAmmo",4)
TNT1 A 0 A_GiveInventory("MirrorBusterFlag", 35)
TNT1 A 0 A_PlaySound("weapon/mirrordeflect2")
stop
}
}

actor MirrorShot3 : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
states
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_FireCustomMissile("MirrorShotWeaker",0,0,0,0)
TNT1 A 0 A_TakeInventory("MirrorBusterAmmo",1)
TNT1 A 0 A_GiveInventory("MirrorBusterFlag", 35)
TNT1 A 0 A_PlaySound("weapon/mirrordeflect")
stop
}
}


actor MirrorShotWeak
{
PROJECTILE
+THRUGHOST
Radius 8
Height 7
scale 2.5
damage (30)
damagetype "MirrorBuster1"
Obituary "$OB_MIRRORBUSTER"
speed 40
States
{
Spawn:
MIRR D 2
loop
}
}

actor MirrorShotWeaker : MirrorShotWeak
{
damage (15)
damagetype "MirrorBuster"
States
{
Spawn:
MIRR C 2
loop
}
}

actor MirrorShotStrong : MirrorShotWeak
{
+RIPPER
damage (40)
damagetype "MirrorBuster2"
States
{
Spawn:
MIRR DE 2
loop
}
}

actor MirrorShotWeakBoss : MirrorShotWeak
{
damage (10)
speed 38
states
{
Spawn:
MIRR D 0
MIRR D 0 ThrustThingZ(0,0,0,0)
MIRR C 1
loop
}
}

actor MirrorShotStrongBoss : MirrorShotStrong
{
damage (30)
speed 50
states
{
Spawn:
MIRR D 0
MIRR D 0 ThrustThingZ(0,0,0,0)
MIRR DE 2
loop
}
}

actor MirrorShotStrongerBoss : MirrorShotStrong
{
damage (20)
speed 80
radius 32
states
{
Spawn:
MIRR D 0
MIRR D 0 ThrustThingZ(0,0,0,0)
MIRR DE 2
loop
}
}

actor MirrorBusterWepBoss : MirrorBusterWep
{
weapon.ammouse 0
+WEAPON.CHEATNOTWEAPON
States
{
Fire:
MIRR F 0 A_JumpIfInventory("EnkerMirrorCount",4,"Fire4")
MIRR F 0 A_JumpIfInventory("EnkerMirrorCount",3,"Fire3")
MIRR F 0 A_JumpIfInventory("EnkerMirrorCount",1,"Fire2")
MIRR F 0 ACS_Execute(5,0)
MIRR FF 8 A_SpawnItemEx("EnkerMelee")
MIRR F 0 ACS_ExecuteAlways(998,0,52)
MIRR FFFFFFFF 8 A_SpawnItemEx("EnkerMelee")
MIRR F 0 A_Stop
MIRR F 0 SetPlayerProperty(0,1,0)
MIRR F 0 A_GiveInventory("EnkerMirrorCount",1)
MIRR F 0 ACS_Execute(5,0,1)
MIRR F 50
MIRR F 0 SetPlayerProperty(0,0,0)
Goto Ready
Fire2:
MIRR F 0 ACS_ExecuteAlways(998,0,52)
MIRR F 0 A_FireCustomMissile("MirrorShotWeakBoss",0,0,0,0)
MIRR F 10 A_PlaySound("weapon/mirrordeflect")
MIRR F 0 ThrustThingZ(0,58,0,1)
MIRR F 0 A_Recoil(-18)
Goto Mirror
Fire3:
MIRR F 0 ACS_ExecuteAlways(998,0,52)
MIRR F 0 A_FireCustomMissile("MirrorShotStrongBoss",0,0,0,0)
MIRR F 0 A_PlaySound("weapon/mirrordeflect2")
MIRR F 0 ThrustThingZ(0,58,0,1)
MIRR F 0 A_Recoil(-18)
Goto Mirror
Fire4:
MIRR F 0 ACS_ExecuteAlways(998,0,52)
MIRR F 0 A_FireCustomMissile("MirrorShotStrongerBoss",0,0,0,0)
MIRR F 0 A_PlaySound("weapon/mirrordeflect2")
Goto Mirror
Mirror:
MIRR F 0 A_TakeInventory("EnkerMirrorCount",999)
MIRR F 0 SetPlayerProperty(0,0,0)
Goto Ready
}
}

actor EnkerMirrorCount : Inventory
{
inventory.amount 1
inventory.maxamount 8
}

actor EnkerMelee
{
PROJECTILE
States
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_Explode(20, 80, 0)
stop
}
}