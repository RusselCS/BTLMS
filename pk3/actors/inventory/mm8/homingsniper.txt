actor HomingSniperWep : MegaBuster 11097
{
//$Category MM8BDM-Weapons
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 4//2
Inventory.Pickupmessage "$PU_HOMINGSNIPER"
Obituary "$OB_HOMINGSNIPER"
weapon.ammotype "HomingSniperAmmo"
inventory.pickupsound "weapon/weaponup"
inventory.icon "HSNIPERI"
+WEAPON.AMMO_OPTIONAL
Scale 2.0
States
{
Spawn:
WEA4 H 1
loop
Ready:
HSNI C 0 ACS_ExecuteAlways(998,0,88)
HSNI C 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
HSNI C 1
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
HSNI C 1
Loop
Fire:
Hold:
HSNI C 0 A_JumpIfNoAmmo("NoAmmo")
HSNI C 0 A_JumpIfInventory("WeaponCharge",18,"Hold1")
HSNI C 1 A_GiveInventory("WeaponCharge",2)
HSNI C 0 A_Refire
Goto Fire1
Hold1:
HSNI C 0 A_JumpIfInventory("WeaponCharge",19,"Charge1")
HSNI C 0 A_PlaySoundEx ("weapon/chargeup","Weapon")
HSNI C 2 A_GiveInventory("WeaponCharge",1)
Charge1:
HSNI C 0 A_JumpIfInventory("WeaponCharge",34,"Charge2")
HSNI F 1 ACS_ExecuteAlways(991,0,89)
HSNI C 0 ACS_ExecuteAlways(991,0,88)
HSNI C 1 A_GiveInventory("WeaponCharge",1)
HSNI C 0 A_Refire
Goto Fire2
Charge2:
HSNI I 1 ACS_ExecuteAlways(991,0,90)
HSNI J 1 ACS_ExecuteAlways(991,0,91)
HSNI C 1 ACS_ExecuteAlways(991,0,88)
HSNI C 0 A_JumpIfInventory("WeaponCharge", 35, 3)
HSNI C 0 A_GiveInventory("WeaponCharge",1)
HSNI C 0 A_GunFlash
HSNI C 0 A_JumpIfInventory("IsBot", 1, "BotFire")
HSNI C 0 A_Refire
Goto Fire3
BotFire:
HSNI C 0 A_Jump(16, "Fire3")
HSNI C 0 A_Refire
Goto Fire3
Fire1:
HSNI C 0 A_TakeInventory("WeaponCharge",500)
HSNI C 0 A_PlaySoundEx("weapon/coilexplode","Weapon")
HSNI C 0 A_FireCustomMissile("HomingSniper",0,1,8,0)
HSNI DE 3
HSNI C 10 
HSNI C 0 A_Refire
Goto Ready+1
Fire2:
HSNI C 0 A_TakeInventory("WeaponCharge",500)
HSNI C 0 A_PlaySoundEx("weapon/coilexplode","Weapon")
HSNI C 0 A_JumpIfInventory("HomingSniperAmmo", 4, "Fire2r")
Goto Fire1
Fire2r:
HSNI C 0 A_TakeInventory("HomingSniperAmmo",4)
HSNI C 0 A_FireCustomMissile("HomingSniper",0,0,8,0)
HSNI C 2
HSNI C 0 A_FireCustomMissile("HomingSniper",random(-5,5),0,8,0,0,random(-4,4))
HSNI DE 2
HSNI C 20
HSNI C 0 A_Refire
Goto Ready+1
Fire3:
HSNI C 0 A_TakeInventory("WeaponCharge",500)
HSNI C 0 A_PlaySoundEx("weapon/coilexplode","Weapon")
HSNI C 0 A_JumpIfInventory("HomingSniperAmmo", 10, "Fire3r")
HSNI C 0 A_JumpIfInventory("HomingSniperAmmo", 4, "Fire2r")
Goto Fire1
Fire3r:
HSNI C 0 A_TakeInventory("HomingSniperAmmo",10)
HSNI C 0 A_FireCustomMissile("HomingSniper",0,0,8,0)
HSNI C 2
HSNI C 0 A_FireCustomMissile("HomingSniper",random(-7,7),0,8,0,0,random(-4,4))
HSNI D 2
HSNI D 0 A_FireCustomMissile("HomingSniper",random(-7,7),0,8,0,0,random(-4,4))
HSNI D 2
HSNI D 0 A_FireCustomMissile("HomingSniper",random(-7,7),0,8,0,0,random(-4,4))
HSNI D 2
HSNI C 0 A_FireCustomMissile("HomingSniper",random(-7,7),0,8,0,0,random(-4,4))
HSNI DE 5
HSNI C 20
HSNI C 0 A_Refire
Goto Ready+1
NoAmmo:
HSNI C 1 ACS_Execute(979,0)
HSNI C 0 A_Refire
Goto Ready+1
Flash:
TNT1 H 5 A_PlaySoundEx("weapon/coilcharge", "Weapon")
TNT1 H 0 A_JumpIfInventory("WeaponCharge",1,"Flash")
stop
}
}

actor HomingSniperAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}


actor HomingSniper : MageStaffFX2
{
+MISSILE
+SEEKERMISSILE
-EXTREMEDEATH
Damagetype "HomingSniper"
Obituary "$OB_HOMINGSNIPER"
Radius 5
Height 5
scale 2.5
damage (18)
speed 20
States
{
Spawn:
HSNM BBCCBBCC 1 A_MStaffTrack
Goto Primed
Primed:
HSNM B 1 A_MStaffTrack
HSNM B 0 A_JumpIfTargetInLOS("See", 0, 1)
HSNM B 1 A_MStaffTrack
HSNM B 0 A_JumpIfTargetInLOS("See", 0, 1)
HSNM C 1 A_MStaffTrack
HSNM C 0 A_JumpIfTargetInLOS("See", 0, 1)
HSNM C 1 A_MStaffTrack
HSNM C 0 A_JumpIfTargetInLOS("See", 0, 1)
loop
See:
HSNM BBBBBB 0 A_MStaffTrack
HSNM B 1
HSNM B 0 A_ScaleVelocity(2.0)//A_SpawnItemEx("HomingSniper2",0,0,0,momx*2,momy*2,momz*2,0,SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEPOSITION)
Goto FastMode
FastMode:
HSNM BC 2 
HSNM B 0 A_SpawnItemEx("HomingSniperLockon",0,0,0,momx*10,momy*10,momz*10,0,SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEPOSITION)
HSNM BC 2 
HSNM B 0 A_SpawnItemEx("HomingSniperLockon",0,0,0,momx*10,momy*10,momz*10,0,SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEPOSITION)
HSNM BC 2 
HSNM B 0 A_SpawnItemEx("HomingSniperLockon",0,0,0,momx*10,momy*10,momz*10,0,SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEPOSITION)
/*HSNM BC 2 
HSNM B 0 A_SpawnItemEx("HomingSniperLockon",0,0,0,momx*10,momy*10,momz*10,0,SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEPOSITION)
*/
Goto See+4
//loop
Death:
MMFX B 0
MMFX B 0 A_GiveToTarget("Clip", 1)
MMFX BCDE 2
stop
}
}

actor HomingSniperLockon
{
PROJECTILE
Radius 16
Height 16
scale 2.5
damage (0)
speed 80
States
{
Spawn:
TNT1 A 1
loop
XDeath:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("HomingCrosshair",8,0,8,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
Death:
TNT1 A 1
stop
}
}


actor HomingLockGiver : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
states
{
Pickup:
Use:
TNT1 A 0 A_SpawnItemEx("HomingCrosshair",8,0,20,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
stop
}
}


actor HomingCrosshair
{
+CLIENTSIDEONLY
+NOINTERACTION
-SOLID
+NOGRAVITY
Scale 2.5
States
{
Spawn:
HSNI KKK 2
stop
}
}